## Summary

`BigBang.sol._borrow()` does not collect borrowing fee and does not mint enough USDO token including borrowing fee.

## Severity

High

## Proof of Concept

In order to collect minting fee, `amount + feeAmount` USDO should be minted, and `feeAmount` should be sent to protocol treasury which is `Penrose.sol`.

So, protocol cannot collect deserved minting fee from user.

https://github.com/Tapioca-DAO/tapioca-bar-audit/blob/master/contracts/markets/bigBang/BigBang.sol#L742-L767
```solidity
function _borrow(
        address from,
        address to,
        uint256 amount
    ) internal returns (uint256 part, uint256 share) {
        uint256 feeAmount = (amount * borrowOpeningFee) / FEE_PRECISION; // A flat % fee is charged for any borrow

        (totalBorrow, part) = totalBorrow.add(amount + feeAmount, true);
        require(
            totalBorrowCap == 0 || totalBorrow.elastic <= totalBorrowCap,
            "BigBang: borrow cap reached"
        );

        userBorrowPart[from] += part;

        //mint USDO
        IUSDOBase(address(asset)).mint(address(this), amount); //@audit -wrong fee collection - fee amount is not minted. nothing to collect

        //deposit borrowed amount to user
        asset.approve(address(yieldBox), amount);
        yieldBox.depositAsset(assetId, address(this), to, amount, 0);

        share = yieldBox.toShare(assetId, amount, false);

        emit LogBorrow(from, to, amount, feeAmount, part);
    }
```

## Recommendation

1) Should mint `amount + feeAmount` USDO token and send `feeAmount` to Penrose treasury.
2) Should collect `feeAmount` as fee

```solidity
function _borrow(
        address from,
        address to,
        uint256 amount
    ) internal returns (uint256 part, uint256 share) {
        uint256 feeAmount = (amount * borrowOpeningFee) / FEE_PRECISION; // A flat % fee is charged for any borrow

        (totalBorrow, part) = totalBorrow.add(amount + feeAmount, true);
        require(
            totalBorrowCap == 0 || totalBorrow.elastic <= totalBorrowCap,
            "BigBang: borrow cap reached"
        );

        userBorrowPart[from] += part;

        //mint USDO
-        IUSDOBase(address(asset)).mint(address(this), amount); // @audit -wrong fee collection - fee amount is not minted. nothing to collect
+        IUSDOBase(address(asset)).mint(address(this), amount + feeAmount); 
+       totalFees += feeAmount;

        //deposit borrowed amount to user
        asset.approve(address(yieldBox), amount);
        yieldBox.depositAsset(assetId, address(this), to, amount, 0);

        share = yieldBox.toShare(assetId, amount, false);

        emit LogBorrow(from, to, amount, feeAmount, part);
    }
```
